apply plugin: 'maven-publish'
apply plugin: 'signing'


task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION




afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll setup later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
//                artifacts javaDocJar

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = "The Best Pdf Viewer Android library"
                    url = 'https://github.com/hamurcuabi/PdfViewer'
                    licenses {
                        license {
                            name = 'MIT license'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }

                    scm {
                        url = "https://github.com/hamurcuabi/PdfViewer"
                        connection = "scm:git:git://github.com/hamurcuabi/PdfViewer.git"
                        developerConnection = "scm:git:ssh://git@github.com/hamurcuabi/PdfViewer.git"
                    }

                    developers {
                        developer {
                            id = 'Hamurcu'
                            name = 'Emre Hamurcu'
                            email = 'emrehamurcu.95@gmail.com'
                        }
                    }
                }
            }
        }
    }
}



signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"]
    )
    sign publishing.publications
}